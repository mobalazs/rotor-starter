' Roku Requests library
import "pkg:/source/roku_modules/rokurequests/Requests.brs"
' Utils from Rotor Framework
import "pkg:/source/roku_modules/rotorframework/utils/GeneralUtils.bs"
import "pkg:/source/roku_modules/rotorframework/utils/NodeUtils.bs"
import "pkg:/source/roku_modules/rotorframework/utils/ArrayUtils.bs"

sub init()
    m.top.functionName = "fetch"
end sub

sub fetch()

    request = m.top.request
    url = request.url

    #if debug
        ' Simulate network delay in debug mode
        sleep(200)
        response = {
            jsonString: ReadAsciiFile(url),
            ok: true,
            statusCode: 200
        }
        json = ParseJson(response.jsonString)
        response.json = json
    #else
        response = Requests().get(url)
    #end if

    ' Note: This is exactly the point where the new Data-Transfer APIs will help:
    ' they provide a more efficient way to pass data between request tasks,
    ' fetch pools, and content readers without blocking the render thread or
    ' relying on heavy rendezvous.
    ' But, until you start using these new APIs in your project, itâ€™s still recommended
    ' to convert responses into ContentNode structures as early as possible, since
    ' they transfer more efficiently between threads and avoid unnecessary
    ' rendezvous overhead.
    parsedResponse = parse(response)

    m.top.response = parsedResponse
end sub

function parse(response) as Object

    json = response.json

    ' Convert to desired node structure
    ' Build a lookup of content nodes by contentId
    itemsById = {}

    for each item in json.items
        cardData = {
            contentId: item.contentId,
            title: item.title,
            shortDescription: item.shortDescription,
            PosterUrl: item.posterUrl,
            FHDPosterUrl: item.posterUrl,
            HDPosterUrl: item.posterUrl
        }

        contentNode = CreateObject("roSGNode", "ContentNode")
        contentNode.setFields(cardData)
        itemsById[item.contentId] = contentNode
    end for

    ' Build listContent suitable for RowList with proper hierarchy:
    ' Root ContentNode containing Row ContentNodes, each Row containing Item ContentNodes
    listContentNode = CreateObject("roSGNode", "ContentNode")
    if type(json.rows) <> "invalid"
        rowIndex = 0
        for each row in json.rows
            rowIndex = rowIndex + 1
            itemNodes = []
            for each cid in row
                if itemsById.Lookup(cid) <> invalid
                    itemNodes.push(itemsById[cid])
                end if
            end for

            ' Create row node with items as children
            rowNode = CreateObject("roSGNode", "ContentNode")
            rowNode.title = "Row " + rowIndex.toStr()
            rowNode.appendChildren(itemNodes)

            listContentNode.appendChild(rowNode)
        end for
    end if

    return {
        listContentNode: listContentNode,
        ok: response.ok,
        statusCode: response.statusCode
    }
end function
