' App specific utils
import "pkg:/source/utils/Utils.bs"

namespace Reducers

    class ContentReaderReducer extends Rotor.Reducer

        sub logIntent(intent)
            Utils.BasicLogger(`[CONTENT_READER] dispatcherId: ${m.ownerDispatcherId}, type: ${intent.type}`)
        end sub

        transferPool = {}

        rows = [
            { titleKeys: "popularMovies", endpoint: "/movie/popular", data: invalid },
            { titleKeys: "topRated", endpoint: "/movie/top_rated", data: invalid },
            { titleKeys: "nowPlaying", endpoint: "/movie/now_playing", data: invalid },
            { titleKeys: "upcoming", endpoint: "/movie/upcoming", data: invalid },
            { titleKeys: "trendingThisWeek", endpoint: "/trending/movie/week", data: invalid }
        ]

        ' ======================
        ' Dispatcher Middlewares
        ' ======================

        override function applyMiddlewares()
            return [
                ' Logger middleware
                function(intent, state) as Intent
                    m.logIntent(intent)

                    return intent ' NEXT
                end function,

                ' Async content reader middleware
                function(intent, state) as Intent
                    if intent.type = IntentTypes.ContentReader.LOAD_CONTENT
                        ' clear previous data
                        for each row in m.rows
                            row.data = invalid
                        end for
                        m.startAsyncRequests()
                        return invalid
                    end if

                    return intent ' NEXT
                end function,

                function(intent, state) as Intent
                    if intent.type = IntentTypes.ContentReader.FETCH_DONE

                        json = intent.payload.json
                        row = intent.payload.context
                        statusCode = intent.payload.statusCode
                        row.data = json.results

                        allFetched = true
                        for each row in m.rows
                            allFetched = allFetched and row.data <> invalid
                        end for

                        if allFetched
                            rowListContent = CreateObject("roSGNode", "ContentNode")
                            for each row in m.rows
                                rowNode = m.createRowWithCards(row.data, row.titleKeys)
                                rowListContent.appendChild(rowNode)
                            end for

                            ' Modify intent
                            intent.type = IntentTypes.ContentReader.CONTENT_LOADED
                            intent.payload = {
                                rowListContent: rowListContent,
                                statusCode: statusCode
                            }
                            return intent ' NEXT
                        else
                            return invalid ' Prevent further processing until all data fetched
                        end if
                    end if

                    return intent ' NEXT
                end function,

                ' Response error middleware
                function(intent, state) as Intent
                    if intent.type = IntentTypes.ContentReader.FETCH_ERROR

                        ' Modify intent
                        intent.type = IntentTypes.ContentReader.CONTENT_LOAD_ERROR
                        intent.payload = {
                            reason: intent.payload.reason,
                            statusCode: intent.payload.statusCode
                        }

                    end if
                    return intent ' NEXT
                end function


            ]
        end function

        ' ==================
        ' Dispatcher Reducer
        ' ==================

        override function reducer(state, intent)
            if intent.type = IntentTypes.ContentReader.CONTENT_LOADED
                state.rowListContent = intent.payload.rowListContent
                state.statusCode = intent.payload.statusCode
                state.ok = true

            else if intent.type = IntentTypes.ContentReader.CONTENT_LOAD_ERROR
                state.statusCode = intent.payload.statusCode
                state.ok = false

            else if intent.type = IntentTypes.ContentReader.SET_LOCALE
                state.locale = intent.payload.locale
                ' Reload home content
                m.dispatch({
                    type: IntentTypes.ContentReader.LOAD_CONTENT
                })
            end if

            return state
        end function


        ' ====================
        ' Async helper methods
        ' ====================

        sub startAsyncRequests()
            appInfo = CreateObject("roAppInfo")
            apiKey = appInfo.GetValue("tmdb_api_key")

            if apiKey = invalid or apiKey = ""
                Utils.BasicLogger("[CONTENT_READER] ERROR: tmdb_api_key not found in manifest")
                return
            end if

            ' Launch all requests using roUrlTransfer
            for each row in m.rows
                locale = m.getState().locale
                locale = locale.Replace("_", "-") ' api requires hyphen format
                url = `https://api.themoviedb.org/3${row.endpoint}?api_key=${apiKey}&language=${locale},en-US&include_image_language=${locale},null&page=1`

                m.transferPool[row.endpoint] = CreateObject("roUrlTransfer")
                transfer = m.transferPool[row.endpoint]
                transfer.SetUrl(url)
                Utils.BasicLogger(`[CONTENT_READER] Starting async request url: ${url}`)

                transfer.SetCertificatesFile("common:/certs/ca-bundle.crt")
                transfer.SetMessagePort(m.port)

                ' Register transfer with framework before async call
                m.registerAsyncTransfer(transfer, row)

                transfer.AsyncGetToString()
            end for
        end sub

        override sub asyncReducerCallback(msg)
            if msg.type = "roUrlEvent"
                event = msg.event
                context = msg.context
                statusCode = event.GetResponseCode()

                if statusCode = 200
                    jsonString = event.GetString()
                    json = ParseJson(jsonString)

                    m.dispatch({
                        type: IntentTypes.ContentReader.FETCH_DONE,
                        payload: {
                            json: json,
                            context: context,
                            statusCode: statusCode
                        }
                    })
                else
                    Utils.BasicLogger(`[CONTENT_READER] HTTP Error ${event.GetResponseCode()}: ${context}`)
                    m.dispatch({
                        type: IntentTypes.ContentReader.FETCH_ERROR,
                        payload: {
                            reason: event.GetFailureReason(),
                            context: context,
                            statusCode: statusCode
                        }
                    })
                end if
            end if
        end sub

        ' ====================
        ' Helper methods
        ' ====================

        function createRowWithCards(cards, rowTitleKey as String) as Object
            rowNode = CreateObject("roSGNode", "ContentNode")
            rowNode.title = rowTitleKey

            for each card in cards
                if card.poster_path <> invalid and card.poster_path <> ""
                    cardNode = CreateObject("roSGNode", "ContentNode")
                    cardNode.title = card.title
                    cardNode.description = card?.overview ?? ""
                    cardNode.HDPosterUrl = `https://image.tmdb.org/t/p/w200` + card.poster_path

                    rowNode.appendChild(cardNode)
                end if
            end for

            return rowNode
        end function

    end class

end namespace
