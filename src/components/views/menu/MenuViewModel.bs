import "MenuItemViewModel.bs"

namespace ViewModels


    class MenuViewModel extends Rotor.ViewModel

        props = {
            ' Note: This menBar viewModel is compatible with dispatcher based on Models.NavState:
            navDispatcherId: "", ' Dispatcher ID for navigation
            activePageId: "" ' Currently active page ID
        }

        viewModelState = {
            currentPageId: "" ' Currently active page ID
        }

        navDispatcher as Rotor.Dispatcher

        override sub onCreateView()
            Utils.BasicLogger("[MenuViewModel] Create menu")
            m.navDispatcher = m.getDispatcher(m.props.navDispatcherId)

            ' Listen for navigation state changes
            m.navDispatcher.addListener({
                mapStateToProps: sub(props, state)
                    props.activePageId = state.activePageId
                end sub,
                callback: m.onCurrentPageIdChanged
            })

            ' Get initial activePageId
            navState = m.navDispatcher.getState() as Models.NavState
            m.props.activePageId = navState.activePageId
        end sub

        override sub onMountView()
            Utils.BasicLogger("[MenuViewModel] Mount menu")
            ' Set initial UI according to state
            m.onCurrentPageIdChanged()
            ' Set initial focus to first menu item
            m.plugins.focus.setFocus()
        end sub

        override sub onDestroyView()
            Utils.BasicLogger("[MenuViewModel] Update menu")
            m.navDispatcher.destroy()
        end sub

        override function template() as object
            navState = m.navDispatcher.getState() as Models.NavState

            ' Build menu items dynamically
            menuItems = []
            for each pageId in navState.pageIds

                pageSettings = navState.pageSettings[pageId]

                menuItems.push({
                    id: `navItem_${pageId}`,
                    viewModel: ViewModels.MenuItemViewModel,
                    props: {
                        pageId: pageId,
                        iconUri: pageSettings.iconUri,
                        intentType: pageSettings.intentType,
                        dispatcherId: m.props.navDispatcherId
                    }
                })
            end for

            return {
                nodeType: "Group",
                focus: {
                    group: {
                        defaultFocusId: "navItem_home",
                        onFocused: sub(isFocused as boolean)
                            ' Handle focus change if needed
                        end sub
                    }
                },
                children: [
                    {
                        id: "menuBackground",
                        nodeType: "Rectangle",
                        fields: {
                            width: 300,
                            height: UI.designResolution.h,
                            color: UI.colors.surface,
                            opacity: 0.5,
                            translation: [0, 0]
                        }
                    },
                    {
                        id: "navigationBar",
                        nodeType: "LayoutGroup",
                        fields: {
                            enableRenderTracking: true,
                            translation: [UI.safeArea.x, UI.designResolution.h / 2 - 200],
                            layoutDirection: "vert",
                            itemSpacings: [20]
                        },

                        ' Set proper width for menu background based on navigation bar width (adapt to different text lengths)
                        observer: {
                            fieldId: "renderTracking",
                            callback: sub(payload)
                                navigationBarNode = m.node
                                menuBackgroundNode = m.getSiblingWidget("menuBackground").node

                                ' Get navigation bar width from bounding rect
                                navBarWidth = navigationBarNode.localBoundingRect().width

                                ' Set menu background width to match navigation bar width
                                menuBackgroundNode.width = navBarWidth + UI.safeArea.x * 2
                            end sub
                        },

                        ' Menu item view models as children
                        children: menuItems
                    }
                ]
            }
        end function

        sub onCurrentPageIdChanged()
            ' Handle current page ID change if needed
            Utils.BasicLogger(`[MenuViewModel] Current page ID changed`)
            if m.viewModelState.currentPageId <> m.props.activePageId
                m.render([
                    {
                        id: "navigationBar/navItem_" + m.viewModelState.currentPageId,
                        props: { isSelected: false }
                    },
                    {
                        id: "navigationBar/navItem_" + m.props.activePageId,
                        props: { isSelected: true }
                    }
                ])
                m.viewModelState.currentPageId = m.props.activePageId
            end if
        end sub

    end class

end namespace
