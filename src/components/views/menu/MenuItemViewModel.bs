namespace ViewModels
    class MenuItemViewModel extends Rotor.ViewModel

        props = {
            pageId: "home",
            iconUri: ""
        }

        viewModelState = {
            isFocused: false
        }

        sub onFocused(isFocused as boolean)
            ' Update focus state and trigger re-render
            m.children.menuIcon.refresh("fields.blendColor")
            m.children.menuLabel.refresh(["fontStyle", "fields.color"])
        end sub

        sub onSelected()
            ' Navigate to page
            m.getDispatcher("navigation").dispatch({
                type: IntentTypes.Navigation.GOTO_PAGE,
                payload: { pageId: m.props.pageId }
            })
        end sub

        override function template() as object

            iconSize = UI.components.menuBar.menuItem.iconSize
            labelOffsetX = iconSize + 10 ' icon width + spacing
            menuPadding = 24

            return {
                nodeType: "Group",
                dispatcher: "navigation",
                focus: {
                    onFocusChanged: sub(isFocused as boolean): typecast m as ViewModels.MenuItemViewModel
                        m.onFocused(isFocused)
                    end sub,

                    onSelected: sub() typecast m as ViewModels.MenuItemViewModel
                        m.onSelected()
                    end sub
                },
                children: [
                    {
                        id: "menuIcon",
                        nodeType: "Poster",
                        fields: {
                            uri: m.props.iconUri,
                            width: iconSize,
                            height: iconSize,
                            loadWidth: iconSize,
                            loadHeight: iconSize,
                            translation: [0, 0],
                            blendColor: function() as string
                                return m.viewModelState.isFocused ? UI.colors.primaryHover : UI.colors.onSurface
                            end function
                        }
                    },
                    {
                        id: "menuLabel",
                        nodeType: "Label",
                        fontStyle: function() as object
                            return m.viewModelState.isFocused ? UI.typography.headlineSmall_bold_aa : UI.typography.headlineSmall_aa
                        end function,
                        fields: {
                            text: `@l10n.menu.${m.props.pageId}`,
                            height: menuPadding + iconSize,
                            translation: [labelOffsetX, 0],
                            color: function() as string typecast m as Rotor.Widget
                                return m.viewModelState.isFocused ? UI.colors.primaryHover : UI.colors.onSurface
                            end function
                        }
                    }
                ]
            }
        end function
    end class
end namespace
