
namespace ViewModels

    class LayoutViewModel extends Rotor.ViewModel

        ' ========== Shared state and props of viewModel ==========

        viewModelState = {
            currentPageId: "",
            pages: {
                "home": ViewModels.HomePage,
                "movies": ViewModels.MoviesPage,
                "settings": ViewModels.SettingsPage
            }
        }

        props = {
            activePageId: "home" ' Initial page
        }

        ' ========== ViewModel Variables ==========

        private navDispatcher as Rotor.Dispatcher


        ' ===========================
        ' ViewModel lifecycle methods
        ' ===========================

        override sub onCreateView()
            Utils.BasicLogger("[LayoutViewModel] Creating layout controller")

            ' Create Navigation dispatcher in render thread (UI-specific state)
            navModel = new Models.NavigationModel()
            navReducer = new Reducers.NavigationReducer()
            Rotor.createDispatcher("navigation", navModel, navReducer)

            ' Get reference to dispatcher
            m.navDispatcher = m.getDispatcher("navigation")

            ' Listen for navigation state changes
            m.navDispatcher.addListener({
                mapStateToProps: sub(props, state)
                    props.activePageId = state.activePageId
                end sub,
                callback: m.onNavigationChanged
            })
        end sub

        override sub onMountView()
            ' Render initial page
            m.onNavigationChanged()
        end sub

        override function template() as object
            return {
                nodeType: "Group",
                id: "mainLayout",
                fields: {
                    translation: [0, 0]
                },
                children: [
                    ' Background
                    {
                        id: "background",
                        nodeType: "Rectangle",
                        fields: {
                            width: UI.designResolution.w,
                            height: UI.designResolution.h,
                            color: UI.colors.background
                        }
                    },
                    ' Page container
                    {
                        id: "pageContainer",
                        nodeType: "Group",
                        fields: {
                            translation: [0, 100]
                        }
                    },
                    ' Navigation bar - left side vertical menu
                    {
                        id: "menu",
                        viewModel: ViewModels.MenuViewModel
                    }
                ]
            }
        end function

        override sub onDestroyView()
            Utils.BasicLogger("[LayoutViewModel] Destroying layout controller")
            m.navDispatcher.destroy()
        end sub

        ' ==========================================
        ' Custom method to handle navigation changes
        ' ==========================================

        private sub onNavigationChanged()
            Utils.BasicLogger(`[LayoutViewModel] Navigation changed: ${m.props.activePageId}`)

            ' Handle page change
            if m.viewModelState.currentPageId <> m.props.activePageId
                m.proceedPageTransition()
                m.viewModelState.currentPageId = m.props.activePageId
            end if
        end sub

        private sub proceedPageTransition()
            pageContainer = m.getWidget("pageContainer")
            pagesToRender = []

            ' Render new active page
            pagesToRender.push({
                id: m.props.activePageId,
                viewModel: m.viewModelState.pages[m.props.activePageId],
                props: {
                    isActive: true
                }
            })
            ' Hide current page if exists
            if pageContainer.children[m.viewModelState.currentPageId] <> invalid
                pagesToRender.push({
                    id: m.viewModelState.currentPageId,
                    props: {
                        isActive: false
                    }
                })
            end if

            ' Render transition
            pageContainer.render({
                children: pagesToRender
            })
        end sub

    end class
end namespace
