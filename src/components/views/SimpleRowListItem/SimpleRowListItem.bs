sub init()
    m.itemposter = m.top.findNode("itemPoster")
    m.itemmask = m.top.findNode("itemMask")
    m.itemlabel = m.top.findNode("itemLabel")
    m.labelBackground = m.top.findNode("labelBackground")

    ' Set initial unfocused scale on the whole Group
    m.top.scale = [0.95, 0.95]
    ' Set scale center to middle of the item
    m.top.scaleRotateCenter = [100.0, 150.0]
end sub

sub showcontent()
    itemcontent = m.top.itemContent
    m.itemposter.uri = itemcontent.HDPosterUrl
    m.itemlabel.text = itemcontent.title
end sub

sub showfocus()
    ' Inverted scaling: focused = 1.0, unfocused = 0.95
    ' focusPercent: 0 (unfocused) -> 1 (focused)
    scale = 0.95 + (m.top.focusPercent * 0.05)
    ' Scale the whole Group so focus border scales too
    m.top.scale = [scale, scale]
end sub

sub showrowfocus()
    m.itemmask.opacity = 0.75 - (m.top.rowFocusPercent * 0.75)
    m.itemlabel.opacity = m.top.rowFocusPercent
    m.labelBackground.opacity = m.top.rowFocusPercent * 0.9
end sub
