import "BasePage.bs"
import "../pickers/SettingSelectorViewModel.bs"

namespace ViewModels
    class SettingsPage extends ViewModels.BasePage

        override function template() as object
            return {
                nodeType: "LayoutGroup",
                fields: {
                    itemSpacings: [20, 30],
                    translation: m.props.pageTranslation
                },
                focusGroup: {
                    defaultFocusId: "exampleButton"
                },
                children: [
                    {
                        id: "pageTitle",
                        nodeType: "Label",
                        fields: {
                            text: "@l10n.pages.settings.title",
                            color: UI.colors.onBackground
                        },
                        fontStyle: UI.typography.displaySmall_aa
                    },
                    {
                        id: "pageContent",
                        nodeType: "Label",
                        fields: {
                            text: "@l10n.pages.settings.content",
                            color: UI.colors.onBackground
                        },
                        fontStyle: UI.typography.bodyLarge_aa
                    },
                    {
                        id: "exampleButton",
                        viewModel: ViewModels.BaseButton,
                        props: {
                            text: "@l10n.buttons.clickMe"
                        },
                        focus: {
                            down: "languageSelector"
                        }
                    },
                    {
                        id: "languageSelector",
                        viewModel: ViewModels.SettingSelectorViewModel,
                        props: {
                            iconUri: UI.components.settings.settingIcon.iconUri.language,
                            labelText: "@l10n.settings.language",
                            pickerStack: m.getViewModel().getMappedLanguages(),
                            initialKey: m.getViewModel().i18n().locale,
                            width: 300,
                            onItemSelected: m.getViewModel().onLanguageSelected
                        }
                    }
                ]
            }
        end function

        override sub onCreateView()
            Utils.BasicLogger("[SettingsPage] Created")
        end sub

        override sub onDestroyView()
            Utils.BasicLogger("[SettingsPage] Destroyed")
        end sub

        private function getMappedLanguages() as Interfaces.PickerObject[]
            mappedLanguages = [] as Interfaces.PickerObject[]
            languages = m.viewModelState.l10n.languages
            for each key in languages
                mappedLanguages.push({
                    key: key,
                    text: languages[key]
                })
            end for
            mappedLanguages.SortBy("key")
            return mappedLanguages
        end function

        private sub onLanguageSelected(selectedItem as Interfaces.PickerObject)
            newLocale = selectedItem.key
            loadLanguage(newLocale)

            subTree = m.getSubtreeClone("./*", ["fields.text"])

            root = m.getRootWidget()
            root.children.appContainer.render(subTree)

            ' Notify Home ContentReader to reload content in new locale
            m.dispatchTo("contentReaderDispatcher", {
                type: IntentTypes.ContentReader.SET_LOCALE,
                payload: {
                    locale: newLocale
                }
            })

        end sub

    end class
end namespace
