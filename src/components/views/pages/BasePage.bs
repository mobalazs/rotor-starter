namespace ViewModels
    class BasePage extends Rotor.ViewModel

        props = {
            isActive: false,
            pageTranslation: [UI.components.menuBar.pageOffset, UI.safeArea.y + UI.components.menuBar.rightPadding]
        }

        override sub onUpdateView()
            Utils.BasicLogger(`[PAGE] Page "${m.id}" is now ${m.props.isActive ? "active" : "inactive"}`)
            ' Animate page visibility
            m.animateIsActiveState()
            ' Delegate focus to page if active
            m.delegateFocusToPage()
        end sub

        override sub onMountView()
            ' Ensure initial opacity state
            m.node.opacity = 0
            ' Animate page visibility
            m.animateIsActiveState()
            ' Delegate focus to page if active
            m.delegateFocusToPage()
        end sub

        sub delegateFocusToPage()
            ' Set focus to defaultFocusId defined by top widget
            if m.props.isActive then m.plugins.focus.setFocus()
        end sub

        sub animateIsActiveState()
            m.animator("pageVisibility").create({
                target: m,
                opacity: m.props.isActive ? 1 : 0,
                duration: UI.motion.durationSlow,
                easeFunction: UI.motion.easingEmphasized,
                delay: m.props.isActive ? UI.motion.durationSlow : 0
            }).play()
        end sub

    end class
end namespace
